import cg.

go =>
    cuba(Os),
    show(Os).

cuba(Os) =>   
  Frame = new_rectangle(),
  Frame.fill #= 0,
  Frame.height #> 200,
  Bg = new_rectangle(),
  Frame.width #= Bg.width,
  Frame.height #= Bg.height,
  Bg.color #= blue,
  Bg.width #= 2*Bg.height,

  W1 = new_rectangle(),
  W2 = new_rectangle(),
  W1.color = white,
  W2.color = white,
  W1.width #= Bg.width,
  W2.width #= Bg.width,
  W1.width #= W2.width,
  W1.height #= W2.height,
  5*W1.height #= Bg.height,  
  W1.x #= Bg.x,
  W1.y #= Bg.y + Bg.height/5,
  W2.x #= Bg.x,
  W2.y #= Bg.y + 3*Bg.height/5,


  Tri = new_triangle(),
  Tri.color = red,
  Tri.point1 #= Bg.leftTopPoint,
  Tri.point2 #= Bg.leftBottomPoint,
  Tri.x3 #= Bg.x + 3*Bg.width/7,
  Tri.y3 #= Bg.y + Bg.height/2,


  Star = new_star(5),
  Star.color #= white,
  Star.angle0 #= 1,
  Star.centerX #= Bg.x + Bg.width/7,
  Star.centerY #= Bg.centerY,
  Star.diameter #= Bg.height/4,
  Os=[Bg,W1,W2,Tri,Star,Frame].
